apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

apply plugin: 'com.google.gms.google-services'

apply plugin: 'com.apollographql.android'

apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion 29
    buildToolsVersion '29.0.2'

    sourceSets {
        main.java.srcDirs += 'src/main/graphql'
    }

    dataBinding {
        enabled = true
    }

    defaultConfig {
        applicationId "com.persidius.eos.aurora"
        minSdkVersion 28
        targetSdkVersion 29
        versionCode 400
        versionName "4.0.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [
                        "room.schemaLocation"  : "$projectDir/schemas".toString(),
                        "room.incremental"     : "true",
                        "room.expandProjection": "true"
                ]
            }
        }
    }
    buildTypes {
        release {
            buildConfigField "String", "SENTRY_DSN", "\"https://52b4afd3232f4655a2d009fc228fe61f@sentry.persidius.com/3\""
            buildConfigField "Boolean", "ENABLE_HTTP_LOGGING", "false"
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            buildConfigField "String", "SENTRY_DSN", "\"https://52b4afd3232f4655a2d009fc228fe61f@sentry.persidius.com/3\""
            buildConfigField "Boolean", "ENABLE_HTTP_LOGGING", "false"
        }
    }
    compileOptions {
        targetCompatibility = '1.8'
        sourceCompatibility = '1.8'
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

apollo {
    generateKotlinModels = true
    customTypeMapping = [
            "Timestamp": "kotlin.lang.Long"
    ]
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.1.0'

    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.1.0'
    implementation 'androidx.lifecycle:lifecycle-reactivestreams:2.1.0'

    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'androidx.recyclerview:recyclerview:1.1.0'

    implementation 'com.google.android.material:material:1.0.0'

    implementation 'androidx.navigation:navigation-fragment:2.1.0'
    implementation 'androidx.navigation:navigation-ui:2.1.0'
    implementation 'androidx.navigation:navigation-fragment-ktx:2.1.0'
    implementation 'androidx.navigation:navigation-ui-ktx:2.1.0'

    implementation 'androidx.preference:preference-ktx:1.1.0'

    implementation 'io.sentry:sentry-android:1.7.23'

    implementation 'com.google.firebase:firebase-core:17.2.1'
    implementation 'com.google.firebase:firebase-ml-vision:24.0.1'
    implementation 'com.google.firebase:firebase-ml-vision-barcode-model:16.0.2'

    implementation 'androidx.camera:camera-core:1.0.0-alpha06'
    implementation 'androidx.camera:camera-camera2:1.0.0-alpha06'
    implementation 'androidx.camera:camera-view:1.0.0-alpha03'

    implementation "androidx.work:work-runtime-ktx:2.2.0"
    implementation "androidx.work:work-rxjava2:2.2.0"

    implementation 'androidx.room:room-runtime:2.2.2'
    implementation 'androidx.room:room-ktx:2.2.2'
    implementation "androidx.room:room-rxjava2:2.2.2"
    kapt 'androidx.room:room-compiler:2.2.2'

    // Eos.kt GraphQL
    implementation 'com.apollographql.apollo:apollo-runtime:1.2.2'
    implementation 'com.apollographql.apollo:apollo-android-support:1.2.2'
    implementation 'com.apollographql.apollo:apollo-rx2-support:1.2.2'

    // Common to both Eos.kt & Auth API
    implementation 'com.squareup.okhttp3:okhttp:3.12.1'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.12.1'

    // Used by the old Auth API
    implementation 'com.squareup.retrofit2:retrofit:2.3.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.3.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.3.0'

    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'
    implementation 'io.reactivex.rxjava2:rxjava:2.2.15'
    implementation 'io.reactivex.rxjava2:rxkotlin:2.4.0'

    implementation 'com.auth0.android:jwtdecode:1.4.0'

    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
}
